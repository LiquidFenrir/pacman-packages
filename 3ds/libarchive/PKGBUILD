# Maintainer:  Dave Murphy <davem@devkitpro.org>

pkgname=3ds-libarchive
pkgver=3.4.3
pkgrel=4
pkgdesc='library that can create and read several streaming archive format (for Nintendo 3DS homebrew development)'
arch=('any')
url="https://www.libarchive.org"
license=('Simplified BSD')
options=(!strip libtool staticlibs)
source=(
 "http://www.libarchive.org/downloads/libarchive-${pkgver}.tar.gz"
 "libarchive-3.4.3.patch"
)
sha256sums=(
 'EE1E749213C108CB60D53147F18C31A73D6717D7E3D2481C157E1B34C881EA39'
 'FF349207BB8C20B1154129D5E507A30E02D4BCE106E91B9F133F2C6F6480964A'
)
makedepends=('3ds-pkg-config' 'devkitpro-pkgbuild-helpers')
depends=('3ds-bzip2' '3ds-zlib' '3ds-liblzma')
groups=('3ds-portlibs')

build() {
  cd libarchive-$pkgver

  source /opt/devkitpro/devkitarm.sh
  source /opt/devkitpro/3dsvars.sh

  patch -Np1 -i "$srcdir"/libarchive-${pkgver}.patch

  ./configure --disable-shared --enable-static --prefix=${PORTLIBS_PREFIX} --host=arm-none-eabi \
    --without-nettle --without-openssl --without-xml2 \
    --without-expat --without-iconv --disable-bsdtar \
    --disable-bsdcpio --disable-bsdcat --disable-acl

  make
}

package() {
  cd libarchive-$pkgver

  source /opt/devkitpro/devkitarm.sh
  source /opt/devkitpro/3dsvars.sh

  make DESTDIR="$pkgdir" install
  # license
  install -Dm644 "$srcdir"/libarchive-${pkgver}/COPYING "$pkgdir"${PORTLIBS_PREFIX}/licenses/$pkgname/COPYING
  # remove useless stuff
  rm -r "$pkgdir"${PORTLIBS_PREFIX}/share
  rm -r "$pkgdir"${PORTLIBS_PREFIX}/bin
}